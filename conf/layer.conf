# We have a conf and classes directory, add to BBPATH
BBPATH .= ":${LAYERDIR}"

# We have recipes-* directories, add to BBFILES
BBFILES += "${LAYERDIR}/recipes-*/*/*.bb \
            ${LAYERDIR}/recipes-*/*/*.bbappend \
            ${LAYERDIR}/recipes-*/*/*/*.bb \
            ${LAYERDIR}/recipes-*/*/*/*.bbappend "

BBFILE_COLLECTIONS += "ics_dm"
BBFILE_PATTERN_ics_dm = "^${LAYERDIR}/"
BBFILE_PRIORITY_ics_dm = "10"

LAYERDEPENDS_ics_dm = "elbb rust-layer swupdate virtualization-layer"
LAYERSERIES_COMPAT_ics_dm = "dunfell"

PREFERRED_PROVIDER_virtual/docker ?= "docker-moby"

# enable curl-native with nghttp2 support for cargo-native(rust)
#
# todo: remove that if rust-bindgen, rust-cbindgen, iot-identity-service are
#       not using cargo directly to download "vendor" dependencies anymore,
#       e.g. when we switch back to use meta-iotedge instead of own recipes
PACKAGECONFIG_append_pn-curl-native = " nghttp2"
# unfortunately the above line introduces a circular dependency error.
# we fix it by disabling cmake-native's download feature.
DEPENDS_remove_pn-cmake-native = "curl-native"
CMAKE_EXTRACONF_append_pn-cmake-native = " -DCMAKE_USE_SYSTEM_LIBRARY_CURL=0 -DCMAKE_USE_OPENSSL=OFF"

# mandatory iot-hub-device-update
CORE_IMAGE_EXTRA_INSTALL_append = " iot-hub-device-update"

# conditional enrollment
CORE_IMAGE_EXTRA_INSTALL_append = "${@bb.utils.contains('DISTRO_FEATURES', 'ics-dm-demo', ' enrollment', '', d)}"

# conditionally iotedge
CORE_IMAGE_EXTRA_INSTALL_append = "${@bb.utils.contains('DISTRO_FEATURES', 'iotedge', ' iotedge-daemon iotedge-cli kernel-modules', '', d)}"
# If we install iotedge we have to add 'virtualization' to 'DISTRO_FEATURES'.
# We can not do this via
# 'DISTRO_FEATURES_append = "${@bb.utils.contains('DISTRO_FEATURES', 'iotedge', ' virtualization', '', d)}"'.
# We then get a recursion error. Also we can not access 'DISTRO_FEATURES'
# directly, since it is not available at layer.conf load time.
# We delay using a special bbclass that simply calls
# 'DISTRO_FEATURES_append = " virtualization"'.
USER_CLASSES_append = " ${@bb.utils.contains('DISTRO_FEATURES', 'iotedge', 'append_virtualization', 'skip_meta_virt_sanity_check', d)}"
