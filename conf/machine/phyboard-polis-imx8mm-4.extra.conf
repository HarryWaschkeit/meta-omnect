###############################################################################
# settings for meta-ics-dm
###############################################################################

# WIC config
WKS_FILE = "ics-dm-os.phytec.wks.in"
WKS_FILES:${MACHINE} = "${WKS_FILE}"
# don't use class wic-helper from meta-phytec
IMAGE_CLASSES:remove = " wic-helper"

# install kernel-devicetree to 'root' partition instead of 'boot'
IMAGE_BOOT_FILES:remove:pn-ics-dm-os-image = " ${KERNEL_DEVICETREE_FN} oftree"
IMAGE_INSTALL:append:pn-ics-dm-os-image = "kernel-devicetree"

# u-boot boot.scr config
KERNEL_BOOTCMD = "booti"
KERNEL_DEVICETREE_FN = "imx8mm-phyboard-polis-rdk.dtb"
UBOOT_ARCH = "arm64"
UBOOT_FDT_ADDR = "fdt_addr"
UBOOT_FDT_LOAD = "1"

# redundant u-boot env
# This is currently preset by meta-phytec, because we use u-boot-imx instead
# of our patched u-boot here.
ICS_DM_PART_OFFSET_UBOOT_ENV1 = "3840"
ICS_DM_PART_OFFSET_UBOOT_ENV2 = "3968"
ICS_DM_PART_SIZE_UBOOT_ENV = "64"

###############################################################################
# settings for meta-phytec / meta-freescale
###############################################################################

# mask fw_env.config configuration of meta-phytec, we generate that ourselves
BBMASK += "/meta-phytec/recipes-bsp/u-boot/libubootenv_%.bbappend"

# - to fix https://github.com/phytec/meta-phytec/issues/8 remove "python" and
#   add "python3"
# - add "bc-native" to fix u-boot-imx "/bin/sh: 1: bc: not found" error
DEPENDS:remove:pn-u-boot-imx = " python"
DEPENDS:append:pn-u-boot-imx = " bc-native python3"

# when IMX_DEFAULT_BSP is not set explicitly, it defaults to 'mainline'.
# the meta-freescale layer then filters out 'mx8mm' from 'MACHINE_OVERRIDES'
# which results in multiple recipes from meta-phytec failing.
IMX_DEFAULT_BSP = "nxp"

# kernel
PREFERRED_PROVIDER:virtual/kernel ?= "linux-imx"

# enable wifi + bt
MACHINE_FIRMWARE:append = "lwb-etsi-firmware"
