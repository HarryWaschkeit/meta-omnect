DISTRO = "ics-dm-os"
DISTRO_NAME = "${DISTRO_NAME_PREFIX}ICS-DM-OS${DISTRO_NAME_SUFFIX}"
DISTROOVERRIDES = "poky:ics-dm-os"
# DISTRO_VERSION is POKY_VERSION (since we include poky conf before our own
# distro settings) appended with a build number.
DISTRO_VERSION_append = ".${ICS_DM_BUILD_NUMBER}"
TARGET_VENDOR = "-icsdm"

# Systemd
INIT_MANAGER ?= "systemd"
# disable sysusers
# Uids/gids created by systemd-sysusers could potentially clash with
# uids/gids introduced by later update images.
PACKAGECONFIG:remove:pn-systemd = "sysusers"

# Persistent journald
DISTRO_FEATURES_DEFAULT_append = " persistent-journal"
JOURNALD_SystemMaxUse ?= "128M"

# Use opkg's ipk
PACKAGE_CLASSES ?= "package_ipk"

# Check if Buildsystem can reach remote sources
CONNECTIVITY_CHECK_URIS ?= "https://github.com/ICS-DeviceManagement "

# Image configuration
IMAGE_FSTYPES = "ext4.gz wic.xz wic.bmap"
# rm kernel from boot partition, we use a bundled initramfs kernel in rootfs
IMAGE_BOOT_FILES_remove = "uImage Image"
# instead of kernel we want the initramfs bundled kernel
IMAGE_INSTALL_remove = "kernel"

# Disable busybox syslog
SRC_URI_remove_pn-busybox = "file://syslog.cfg"

# define partition (default) sizes in KiB
#     Note, the last data partition will be expanded during the first boot after image flashing inside the INITRAMFS.
#     This means, ICS_DM_PART_SIZE_DATA is the initial value.
ICS_DM_PART_SIZE_BOOT ?= "131072"
ICS_DM_PART_SIZE_ROOTFS ?= "760832"
ICS_DM_PART_SIZE_FACTORY ?= "131072"
ICS_DM_PART_SIZE_ETC ?= "131072"
ICS_DM_PART_SIZE_DATA ?= "524288"

# limit max rootfs size
IMAGE_ROOTFS_MAXSIZE = "${ICS_DM_PART_SIZE_ROOTFS}"
# overhead doesn't make much sense in a read-only-roots
# we minimize the overhead factor (needed for fs) so we don't reach maxsize
IMAGE_OVERHEAD_FACTOR = "1.1"

# initramfs
INITRAMFS_FSTYPES = "cpio.gz"
INITRAMFS_IMAGE = "ics-dm-os-initramfs"
INITRAMFS_IMAGE_NAME = "${DISTRO_NAME}_${DISTRO_VERSION}_${MACHINE}-initramfs.rootfs"
INITRAMFS_IMAGE_BUNDLE = "1"

# enable curl-native with nghttp2 support for cargo-native(rust)
#
# todo: remove that if iot-identity-service is
#       not using cargo directly to download "vendor" dependencies anymore,
#       e.g. when we switch back to use meta-iotedge instead of own recipes
PACKAGECONFIG_append_pn-curl-native = " nghttp2"
# unfortunately the above line introduces a circular dependency error.
# we fix it by disabling cmake-native's download feature.
DEPENDS_remove_pn-cmake-native = "curl-native"
CMAKE_EXTRACONF_append_pn-cmake-native = " -DCMAKE_USE_SYSTEM_LIBRARY_CURL=0 -DCMAKE_USE_OPENSSL=OFF"
